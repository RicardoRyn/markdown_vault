# s1.PPI.sh

利用`s1.PPI.sh`脚本来做PPI

在FC_demo文件夹中的scripts文件夹中打开该脚本，该脚本分为3个部分，如下：

```bash
#!/bin/tcsh

# note location of scripts and data
set basedir =/mnt/hgfs/G/FC_demo
set scriptdir = $basedir/scripts
set subj = sub-01
set subjdir   = $basedir/"$subj".results


# ----------------------------------------
# do all of the work in the $subj.results directory...
cd $subjdir


# ----------------------------------------第一部分：创建兴趣区，提取兴趣的时间序列
# generate seed time series, ppi.seed.1D
# start with primary visual cortex V1
set seed = v1  # 创建兴趣区/种子区/A脑区，本例是在初级视觉皮层处（V1）
echo -28 -96 -6 | 3dUndump -xyz -srad 5 -master stats."$subj"_REML+tlrc -prefix mask.$seed.nii.gz -  # 选择一个坐标，-srad 5 指五毫米为半径的球形ROI
# generate ppi.seed.1D (note that mask dset is unneeded, but visually useful)
3dmaskave -quiet -mask mask.$seed.nii.gz errts."$subj"_REML+tlrc > ppi.seed.$seed.1D  # 提取兴趣区的时间序列。需要注意这里提取的时间序列并不是原始的BOLD信号的时间序列，而是做完GLM后得到的残差。
# 为什么要用残差回归呢？简单说就是PPI是在衡量实验条件和生理反应的交互作用，那么实验条件所引起的主效应都不应该包含在内，而残差是GLM后所有回归都不能解释的成分，即排除了实验条件主效应的条件，所以该用残差回归（errts.开头的文件）。



# ===========================================================================第二部分：根据创建的兴趣区的时间序列和实验条件来得到PPI的回归因子
# generate PPI regressors from seed and timing files
# (script uses 'set seed = ppi.seed.1D')

tcsh $scriptdir/cmd.ppi.2.make.regs $seed  # 这里是单独调用名为cmd.ppi.2.make.regs的脚本来创建PPI的回归因子，后面跟个种子区的标签。具体内容看下一个脚本。

# and copy the results into the stimuli directory
cp work.$seed/p6.* ppi.seed.$seed.1D stimuli  # 将最终得到的PPI的回归因子拷贝到该文件夹下

# and just to see consider:
#    1dplot -one ppi.seed.1D work.v1/p7.v1.sum.PPI.1D
#    1dplot ppi.seed.1D work.v1/p6.*


# ===========================================================================第三部分：得到回归因子之后做解卷积，这样每个实验条件对应的PPI回归因子，都会生成对应PPI的β值，即PPI的系数
# create and run a 3dDeconvolve command for the PPI
# (still run from $subjdir)

# create the 3dDeconvolve command, proc.3dd.ppi.post.full
#tcsh $scriptdir/cmd.ppi.3.ap.post

# and run it
tcsh $scriptdir/proc.3dd.ppi.post.full $subj $seed  # 单独调用proc.3dd.ppi.post.full来进行解卷积操作。具体内容看下下个脚本。



# ===========================================================================
# comments...

# - this data is not designed to capture a PPI effect
# - the results are in PPI.full.stats.FT+tlrc
```

接下来就可以运行s1.PPI.sh脚本了，即完成PPI



---

# cmd.ppi.2.make.regs

查看`cmd.ppi.2.make.regs`脚本，内容如下：

```bash
#!/bin/tcsh


# - basis functions should be consistent across classes
#   i.e. should not mix GAM/BLOCK
# - SPMG1 is okay, but no multi-component functions (AM/DM/IM are okay)

# generate PPI regressors: no 3dD commands

# inputs
#   - stim timing files/labels/durations
#   - PPI label (e.g. Lamy.test1)
#   - seed time series (afni_proc.py can generate example)
#   - NT per run, TR, TRnup  # 以下部分可以不用太去管它，就是设定一些变量的值
set seedlabel = $1

set stim_files = ( stimuli/AFNI_timing.times.*.txt )
set stim_labs = ( VpAp VpAn VnAp VnAn catch )
set stim_dur  = ( 3 3 3 3 3 )       # durations, in seconds

set seed = ppi.seed.$seedlabel.1D
set plabel = $seedlabel

set basis = BLOCK               # matches basis type in main analysis

set NT = ( 590 590 590 )        # num time points per run
set TR = 1.0
set TRnup = 10                  # oversample rate  # TR进行十等分设定，可以自行设定

# compute some items
# rcr - validate TRup (TR must be an integral multiple of TRup)
set TRup = 0.1          # basically TR/TRnup  # TR比TRnup比例为1比10
set demean_psych = 0    # usually 0 (for comparison, should not matter)

set nstim = $#stim_files
set run_lens = ( 590 590 590 )  # run lengths, in seconds

set workdir = work.$plabel
set timingdir = timing.files

# =================================================================
# create work directory, copy inputs, enter

if ( -d $workdir ) then

   # for convenience, but not recommended as a default
   # echo "** removing old work dir $workdir..."
   # rm -fr $workdir

   # these make a safer default
   echo "** will not overwrite PPI work directory $workdir, failing..."
   exit 1
endif

# create output directories and copy inputs there
mkdir $workdir
mkdir $workdir/$timingdir

cp -pv $stim_files $workdir/$timingdir
cp -pv $seed $workdir
set seed = $seed:t

set bind = 0
cd $workdir


# =================================================================  # 这里看该脚本主要是对时间序列怎么进行操作的
# generate ideal IRF  # 这里创建一个脉冲响应函数（或者HRF血液动力学响应函数，这两者在fMRI语境里是同一样东西），指大脑接受到一个时间无尽接近于0的刺激的时候，BOLD信号的变化（即理想情况下BOLD信号的变化）
#  可以在做完PPI的文件夹中（sub-01.results）打开，键入：1dplot work.v1/x.BLOCK.1D。即可查看
#
# This generates the impulse response function for the deconvolution
# and recovolution steps.  It is the expected response to a ~zero
# duration event.

if ( $basis == GAM ) then

   # number of time points = duration / upsampled TR
   set dur = 12         # use a 12 second curve for GAM
   set nt_irf = `ccalc -i "$dur/$TRup"`

   set hrf_file = x.GAM.1D
   3dDeconvolve -nodata $nt_irf 0.1 -polort -1  \
                -num_stimts 1                   \
                -stim_times 1 1D:0 GAM          \
                -x1D $hrf_file -x1D_stop

else if ( $basis == BLOCK ) then

   # number of time points = duration / upsampled TR
   set dur = 15         # use a 15 second curve for BLOCK
   set nt_irf = `ccalc -i "$dur/$TRup"`

   set hrf_file = x.BLOCK.1D
   3dDeconvolve -nodata $nt_irf 0.1 -polort -1    \
                -num_stimts 1                     \
                -stim_times 1 1D:0 "BLOCK(0.1,1)" \
                -x1D $hrf_file -x1D_stop

else
   echo "** invalid basis $basis, should be BLOCK or GAM (or SPMG1)"
   exit 1
endif


# =================================================================  # 将时间文件每一秒十等分，也可以自行设定其他比例。如果你的TR比较大，例如2s、3s、4s，这样将每一秒十等分能够减少误差
# 键入：1dplot p1.v1.01.VpAp_r01.1D。即可查看第一个条件的第一个run为例
# create timing partition files

@ bind ++
set prefix = p$bind.$plabel
set timing_prefix = $prefix

foreach sind ( `count -digits 1 1 $nstim` )
   set sind2 = `ccalc -form '%02d' $sind`
   set tfile = $timingdir/$stim_files[$sind]:t
   set label = $stim_labs[$sind]

   if ( ! -f $tfile ) then
      echo "** missing timing file $tfile"
      exit 1
   endif

   timing_tool.py -timing $tfile                \
         -tr $TRup -stim_dur $stim_dur[$sind]   \
         -run_len $run_lens                     \
         -min_frac 0.3                          \
         -timing_to_1D $timing_prefix.$sind2.$label \
         -per_run_file -show_timing 

   # optionally replace psychological variables with de-meaned versions
   if ( $demean_psych ) then
      set mean = `cat $timing_prefix.$sind2.* | 3dTstat -prefix - 1D:stdin\'`
      echo "-- mean of psych '$label' files = $mean"
      foreach file ( $timing_prefix.$sind2.$label* )
         1deval -a $file -expr "a-$mean" > rm.1D
         mv rm.1D $file
      end
   endif
end


# =================================================================  # 同样将种子区时间文件的横坐标进行upsample一下，即时间十等分
# 键入：1dplot p2.v1.seed.10.r1.1D。可以查看种子区时间文件，它的形状不会有变化，只是横坐标扩大十倍而已
# upsample seed

@ bind ++
set prefix = p$bind.$plabel

# break into n runs

@ rend   = - 1
foreach rind ( `count -digits 1 1 $#NT` )
   @ rstart = $rend + 1  # start after prior endpoint
   @ rend += $NT[$rind]
   1dcat $seed"{$rstart..$rend}" | 1dUpsample $TRnup stdin: \
         > $prefix.seed.$TRnup.r$rind.1D
end

set seed_up = $prefix.seed.$TRnup.rall.1D
cat $prefix.seed.$TRnup.r[0-9]*.1D > $seed_up

# =================================================================  # 用获得的种子区时间序列做一个反卷积（解卷积/去卷积）。是利用之前生成的IRF的形状进行的解卷积
# 键入：1dplot p3.v1.neuro.r1.1D。
# deconvolve

set pprev = $prefix
@ bind ++
set prefix = p$bind.$plabel
set neuro_prefix = $prefix

foreach rind ( `count -digits 1 1 $#NT` )
   3dTfitter -RHS $pprev.seed.$TRnup.r$rind.1D                  \
             -FALTUNG $hrf_file temp.1D 012 -2  \
             -l2lasso -6
   1dtranspose temp.1D > $prefix.neuro.r$rind.1D
end


# ===========================================================================
# partition neuro seeds

set pprev = $prefix
@ bind ++
set prefix = p$bind.$plabel

foreach sind ( `count -digits 1 1 $nstim` )
   set sind2 = `ccalc -form '%02d' $sind`
   set slab = $sind2.$stim_labs[$sind]

   foreach rind ( `count -digits 1 1 $#NT` )
      set neuro_seed = $neuro_prefix.neuro.r$rind.1D
      set rind2 = `ccalc -form '%02d' $rind`
      @ nt = $NT[$rind] * $TRnup

      # note partition files: 1 input, 2 outputs
      set stim_part = $timing_prefix.${slab}_r$rind2.1D
      set neuro_part = $prefix.a.$slab.r$rind.neuro_part.1D
      set recon_part = $prefix.b.$slab.r$rind.reBOLD.1D

      1deval -a $neuro_seed -b $stim_part -expr 'a*b' > $neuro_part  # 用之前解卷积的形状 乘以 时间刺激呈现的时间序列。得到的新的时间序列
      # 键入：1dplot p4.v1.a.01.VpAp.r1.neuro_part.1D。这个图就是PPI（交互作用的）回归因子
      # 也可以同时查看之前解卷积的形状和刺激的时间序列，键入：1dplot -sepscl p4.v1.a.01.VpAp.r1.neuro_part.1D p3.v1.neuro.r1.1D p1.v1.01.VpAp_r01.1D

      waver -FILE $TRup $hrf_file -input $neuro_part -numout $nt > $recon_part
   end

   # and generate upsampled seeds that span runs
   cat $prefix.b.$slab.r*.reBOLD.1D > $prefix.d.$slab.rall.reBOLD.1D
end

# and generate corresponding (reBOLD) seed time series
foreach rind ( `count -digits 1 1 $#NT` )
   set neuro_seed = $neuro_prefix.neuro.r$rind.1D
   waver -FILE $TRup $hrf_file -input $neuro_seed -numout $nt \
         > $prefix.c.seed.r$rind.reBOLD.1D
end

# to compare with $seed_up
3dMean -sum -prefix - $prefix.d.[0-9]*.1D > $prefix.d.task.rall.reBOLD.1D
cat $prefix.c.seed.r*.reBOLD.1D > $prefix.d.seed.rall.reBOLD.1D
echo == can compare upsampled seeds: \
	$seed_up $prefix.d.{seed,task}.rall.reBOLD.1D
set seed_rebold_up = $prefix.d.seed.rall.reBOLD.1D


# ===========================================================================  # 对于最后交互作用的回归因子，需要进行downsample，即将横坐标缩小十倍

# downsample

set pprev = $prefix
@ bind ++
set prefix = p$bind.$plabel

foreach rind ( `count -digits 1 1 $#NT` )
   set neuro_seed = $neuro_prefix.neuro.r$rind.1D
   @ nt = $NT[$rind] * $TRnup

   foreach sind ( `count -digits 1 1 $nstim` )
      set sind2 = `ccalc -form '%02d' $sind`
      set recon_part = $pprev.b.$sind2.$stim_labs[$sind].r$rind.reBOLD.1D
      set recon_down = $prefix.$sind2.$stim_labs[$sind].r$rind.PPIdown.1D

      1dcat $recon_part'{0..$('$TRnup')}' > $recon_down
   end

   # and downsample filtered seed time series
   1dcat $seed_rebold_up'{0..$('$TRnup')}' > $seed:r.reBOLD.1D
end


# ===========================================================================  # 原来的3个run是分开做的，这一步就是把3个run合并在一起
# catentate across runs: final PPI regressors

set pprev = $prefix
@ bind ++
set prefix = p$bind.$plabel

foreach sind ( `count -digits 1 1 $nstim` )
   set sind2 = `ccalc -form '%02d' $sind`
   set slab = $sind2.$stim_labs[$sind]

   cat $pprev.$slab.r*.PPIdown.1D > $prefix.$slab.rall.PPI.1D
end

# =================================================================
# make a final comparison time series

set pprev = $prefix
@ bind ++
set prefix = p$bind.$plabel

3dMean -sum -prefix - $pprev.* > $prefix.sum.PPI.1D

echo "== can compare original seed to sum of PPI regressors:"
echo "   1dplot -one $seed $prefix.sum.PPI.1D"

echo ""
echo "== final PPI regressors: " $seed $pprev.*
echo "   (copy to stimuli dir)"
echo ""
```

---

# proc.3dd.ppi.post.full

```bash
#!/bin/tcsh

set subj = $1
set seed = $2
set prefix_3dd = PPI.$seed.

# ------------------------------
# run the regression analysis
3dDeconvolve -input pb04.$subj.r*.scale+tlrc.HEAD                     \  # 输入预处理之后的文件
    -censor motion_${subj}_censor.1D                                  \
    -ortvec motion_demean.1D mot_demean                               \
    -polort 3                                                         \
    -num_stimts 11                                                     \
    -stim_times 1 stimuli/AFNI_timing.times.1.txt 'BLOCK(3,1)'                   \  # 每个时间刺激呈现的文件
    -stim_label 1 VpAp                                                 \
    -stim_times 2 stimuli/AFNI_timing.times.2.txt 'BLOCK(3,1)'                   \
    -stim_label 2 VpAn                                                 \
    -stim_times 3 stimuli/AFNI_timing.times.3.txt 'BLOCK(3,1)'                   \
    -stim_label 3 VnAp                                                 \
    -stim_times 4 stimuli/AFNI_timing.times.4.txt 'BLOCK(3,1)'                   \
    -stim_label 4 VnAn                                                 \
    -stim_times 5 stimuli/AFNI_timing.times.5.txt 'BLOCK(3,1)'                   \
    -stim_label 5 catch                                                 \
    -stim_file 6 stimuli/p6.$seed.01.VpAp.rall.PPI.1D                   \  # 每个实验条件对应的PPI的回归因子，即刚刚生成的每个条件PPI的回归因子
    -stim_label 6 PPI.VpAp                                             \
    -stim_file 7 stimuli/p6.$seed.02.VpAn.rall.PPI.1D                   \
    -stim_label 7 PPI.VpAn                                             \
    -stim_file 8 stimuli/p6.$seed.03.VnAp.rall.PPI.1D                   \
    -stim_label 8 PPI.VnAp                                             \
    -stim_file 9 stimuli/p6.$seed.04.VnAn.rall.PPI.1D                   \
    -stim_label 9 PPI.VnAn                                             \
    -stim_file 10 stimuli/p6.$seed.05.catch.rall.PPI.1D                   \
    -stim_label 10 PPI.PW                                             \
    -stim_file 11 stimuli/ppi.seed.$seed.1D                                  \  # 开始生成的种子区残差的时间序列
    -stim_label 11 PPI.seed                                            \
    -jobs 8                                                           \
    -gltsym 'SYM: VpAp -VnAn'                                           \  # 在被试水平进行不同条件间的对比
    -glt_label 1 P-N                                                 \
    -gltsym 'SYM: PPI.VpAp -PPI.VnAn'                                   \
    -glt_label 2 PPI:P-N                                              \
    -fout -tout -x1D ${prefix_3dd}X.xmat.1D -xjpeg ${prefix_3dd}X.jpg \
    -x1D_uncensored ${prefix_3dd}X.nocensor.xmat.1D                   \
    -errts ${prefix_3dd}errts.${subj}                                 \
    -bucket ${prefix_3dd}stats.$subj

```